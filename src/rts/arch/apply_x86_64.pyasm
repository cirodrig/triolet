
///////////////////////////////////////////////////////////////////////////////
// Apply functions.
//
// All functions apply 'obj' to 'arg', and either return a new object or
// write their return value to the given address.
procedure apply_i64_f (owned obj, int64 arg) -> owned {
  data pointer ret;
  data int64 arg_ref;
  !arg_ref = arg;
  () = () primcall apply_word(obj, arg_ref, ret);
  owned load ret;
};

procedure apply_i64 (owned obj, int64 arg, pointer ret) -> () {
  data int64 arg_ref;
  !arg_ref = arg;
  () primcall apply_word(obj, arg_ref, ret);
};

procedure apply_i32_f (owned obj, int32 arg) -> owned {
  data pointer ret;
  data int64 arg_ref;
  !arg_ref = arg; // Store 32-bit int in low bits of 64-bit int
  () = () primcall apply_word(obj, arg_ref, ret);
  owned load ret;
};

procedure apply_i32 (owned obj, int arg, pointer ret) -> () {
  data pointer arg_ref;
  !arg_ref = arg; // Store 32-bit int in low bits of 64-bit int
  () primcall apply_word(obj, arg_ref, ret);
};

procedure apply_f32_f (owned obj, float arg) -> owned {
  data pointer ret;
  data int64 arg_ref;
  !arg_ref = arg; // Store 32-bit float in low bits of 64-bit int
  () = () primcall apply_word(obj, arg_ref, ret);
  owned load ret;
};

procedure apply_f32 (owned obj, float arg, pointer ret) -> () {
  data pointer arg_ref;
  !arg_ref = arg; // Store 32-bit float in low bits of 64-bit int
  () primcall apply_word(obj, arg_ref, ret);
};

procedure apply_word (owned obj, pointer arg_ref, pointer ret) -> () {
  obj_info = obj @! ObjectHeader.info;
  obj_tag = obj_info @! InfoTableHeader.tag;

  if (obj_tag == TAG_FUN) {
    arity = uint16 primcall fun_arity(obj);
    if (arity == uint16 1) {
      // Calling a function with one argument.  Call the function now.
      () primcall (obj_info @! FunInfo(value int 0).inexact) (obj, arg_ref, ret);
    }
    else if (arity == uint16 0) {
      // TODO: Handle nullary functions
      () primcall exit (int -1);
    }
    else {
      // Undersaturated application
      // Create a PAP
      typedef my_pap = PAP(value int 0);
      new_pap = owned primcall pap_new (obj, uint16 1, uint16 1);
      POINTER_ARG_GET(arg, arg_ref);
      POINTER_ARG_PUT(new_pap @ my_pap.args as pointer, arg);
      !ret = new_pap;
      ();
    };
  } else if (obj_tag == TAG_PAP) {
    // Determine PAP's arity
    arity = uint16 primcall pap_arity(obj);
    typedef my_pap = PAP(value int 0);

    // Add argument to PAP
    new_pap = owned primcall pap_grow(obj, uint16 1);
    arg_offset = (obj @! my_pap.arg_size) as uint * PAP_WORDSIZE;
    POINTER_ARG_GET(arg, arg_ref);
    POINTER_ARG_PUT(new_pap @ my_pap.args ^+ arg_offset as int, arg);

    if (new_pap @! my_pap.nargs == arity) {
      // Function is fully applied
      () primcall pap_call (new_pap, ret);
    } else {
      // Return the new PAP
      !ret = new_pap;
      ();
    };
  }
  else {
    // Invalid tag
    () primcall exit (int -1);
  };
};

// Apply a value to the unit type
procedure apply_unit (owned obj, pointer ret) -> () {
  obj_info = obj @! ObjectHeader.info;
  obj_tag = obj_info @! InfoTableHeader.tag;

  if (obj_tag == TAG_FUN) {
    arity = uint16 primcall fun_arity(obj);
    if (arity == uint16 1) {
      // Calling a function with one argument.  Call the function now.
      () primcall (obj_info @! FunInfo(value int 0).inexact) (obj, null, ret);
    }
    else if (arity == uint16 0) {
      // TODO: Handle nullary functions
      () primcall exit (int -1);
    }
    else {
      // Undersaturated application
      // Create a PAP
      typedef my_pap = PAP(value int 0);
      new_pap = owned primcall pap_new (obj, uint16 1, uint16 0);
      !ret = new_pap;
      ();
    };
  } else if (obj_tag == TAG_PAP) {
    // Determine PAP's arity
    arity = uint16 primcall pap_arity(obj);
    typedef my_pap = PAP(value int 0);

    // Add argument to PAP
    new_pap = owned primcall pap_grow(obj, uint16 1);

    if (new_pap @! my_pap.nargs == arity) {
      // Function is fully applied
      () primcall pap_call (new_pap, ret);
    } else {
      // Return the new PAP
      !ret = new_pap;
      ();
    };
  }
  else {
    // Invalid tag
    () primcall exit (int -1);
  };
};
